Рівень 2

#Риба-хижак (Рибожер)

__Передмова:__
Ми створимо гру Рибожер. Керуйте великою хижою рибиною так, щоб вона спіймала усю здобич, яка плаває поруч.

##Крок 1: Створення спрайта, який змінює образи.
__Давайте створимо голодну, ненажерливу рибину, що плаває в морі.__

1. Створіть новий проект у Скертч.
2. Оберіть Сцену, потім виберіть вкладку Нове тло (New background) та імпортуйте картинку "під водою" з папки Природа (nature/underwater). Видаліть порожній фон.
3. Змініть назву Спрайту1 на Голодна риба або просто Рибожер.
4. Імпортуйте для Спрайта1 образ голодної риби через вкладку Образи / Новий образ і видаліть попередні образи 1 і 2. Скористайтесь кнопкою тип обертання ліворуч-праворуч у вкладці Рух, щоб упевнитись, що риба рухається лише таким чином або натисніть у полі спрайта на верхній лівий куток значка спрайта та оберіть стиль обертання.
5. Тепер створіть скрипт для Голодної риби, за яким вона рухатиметься у морі, слідуючи руху мишки.

```scratch

	коли натиснуто зелений прапорець
	завжди		
		слідувати за вказіником миші
		переміститись на 3 кроки
	(кінець завжди)
```

###Протестуйте свій проект.
__Натисніть на зелений прапорець.__ 
MПереміщайте курсор по морю.  Чи слідує риба за курсором? 
Що стається, коли ви перестаєте переміщати курсор і риба наздоганяє його? На що це схоже? Чому так відбувається?

7. Можна припинити хаотичне обертання рибини, якщо дозволити її рух на певній відстані від вказівника миші (блок __відстань до__ у вкладці Датчики).

```scratch

	коли натиснуто зелений прапорець
	завжди якщо відстань до вказівника миші > 10
		слідувати за вказіником миші
		переміститись на 3 кроки
	(кінець завхжди)
```


###Протестуйте свій проект.

Збережіть свій проект.

##Спробуйте:

Якщо захочете, ви можете спробувати прописувати різну відстань у скрипті. Як це впливає на рух рибини? Змініть "відстань до" на >100 або на >1.  Змініть кількість кроків переміщення на 20 або на 1 чи 0.


##Крок 2: Створення здобичі

1. Створіть новий спрайт, обравши у папці Тварини відповідне зображення (лобстер, наприклад).
2. За допомогою кнопки зменшення розмірів спрайту, зменшіть його.
3. Створіть скрипт, за яким здобич буде рухатись в морі.  Необхідно, щоб здобич рухалась випадковим чином, тож хай лоб стер спочатку рухається вперед, потім несподівано поверне вліво чи вправо, потім повторить це.

```scratch

	коли натиснуто зелений прапорець
	завжди		
		переміститись на 2 кроки
		повернутись на вибрати випадкове від -20 до 20 градусів
		якщо границя, відбити
	(кінець завжди)
```

###Протестуйте свій проект.
__Натисніть на зелений прапорець і подивіться, як рухається здобич.__ Чи рухається спрайт так, як ви очікували? Чи реалістично це виглядає?

__Поки що Голодна рибина ніяк не взаємодіє зі здобиччю.  У наступному кроці ми це виправимо.__

Збережіть свій проект.

###Спробуйте:

* Змінювати числа у блоці "випадкове від до". Як це впливає на рух здобичі?
* Яка функція блоку __якщо границя, відбити?__ Видаліть цей блок і подивіться, що відбуватиметься.

##Крок 3: Голодна рибина пожирає здобич.

__Тепер ми зробимо так, щоб Голодна рибина з'їдала здобич!__ Коли Голодна рибина спіймала ротом здобич, повинні відбуватись дві речі:
* Голодна рибина повинна закрити рота, видавши при цьому звук "чавкання".
* Здобич повинна щезнути, а через кілька секунд з'явитись знову.

1. Спершу зробимо так, щоб здобич щезала, коли її торкнеться Голодна рибина, а потім знову з'являлась через 3 секунди. У скрипті здобичі скористаємось блоком "доторкається" вкладки Датчики, щоб з'ясувати, чи торкнулась Голодна рибина здобичі.

```scratch

	коли натиснуто зелений прапорець
	завжди		
		переміститись на 2 кроки
		повернутись на вибрати випадкове від -20 до 20 градусів
		якщо границя, відбити
		якщо доторкається до Голодної риби?
			сховати
			чекати 3 сек
			показати
		(кінець якщо)
	(кінець завжди)
```

###Протестуйте свій проект.
__Запустіть гру знову і подумайте, чи все у ній в порядку.__ Зауважте, що здобич зникає незалежно від того, як вона торкнеться рибини. Рибина ж може почекати 3 секунди і з'їсти здобич в той же момент, коли та з'явиться – це нечесно!

1. Як же зробити так, щоб здобич щезала тоді, коли риба торкнеться її ротом?  Можна використати блок "доторкається кольору" і з'ясувати, чи торкнулась здобич блакитних зубів Голодної риби.  Для цього замініть блок "доторкається" на блок "доторкається кольору" у скрипті здобичі, клікніть на блок колір, а потім клікніть на зуби рибини.
2. Потім можна зробити так, щоб здобич допливала до якоїсь випадкової точки на екрані перед тим, як знову з'явитися, за допомогою блоку "переміститись в" і присвоєння випадкових значень х та у.

```scratch

	коли натиснуто зелений прапорець
	завжди		
		переміститись на 2 кроки
		повернутись на вибрати випадкове від -20 до 20 градусів
		якщо границя, відбити
		якщо доторкається кольору []?
			сховати
			чекати 3 сек
			переміститись до x:випадкове від -220 до 220 y: вибрати випадкове від -170 до 170
			показати
		(кінець якщо)
	(кінець завжди)
```
###Протестуйте свій проект.

Знову запустіть гру. Здобич просто зникає, коли торкається рота Голодної риби? Чи з'являється потім здобич у випадковій точці екрану, а не втому ж місці, де її з'їли?

3. Голодна рибина повинна знати, що вона когось з'їла для того, щоб видати звук "чавкання" та змінити колір. Для цього здобич повинна повідомити, що її з'їли перед тим, як щезнути, за допомогою блоку "оповістити" вкладки Керувати.

```scratch

	коли натиснуто зелений прапорець
	завжди		
		переміститись на 2 кроки
		повернутись на вибрати випадкове від -20 до 20 градусів
		якщо границя, відбити
		якщо доторкається кольору []?
			оповістити мене спіймали
			сховати
			чекати 3 сек
			переміститись до x:випадкове від -220 до 220 y: вибрати випадкове від -170 до 170
			показати
		(кінець якщо)
	(кінець завжди)
```
__Тепер потрібно зробити так, щоб Голодна рибина відповідала на повідомлення "мене з'їли" звуком "чавкання" і клацанням щелеп.__

1. Додайте образ клацання щелеп і звук "чавкання" до спрайту Голодної риби.
2. Потім додайте новий скрипт, щоб Голодна риба могла відповідати на повідомлення здобичі.  Цей скрипт змусить рибину "почавкати", клацнути щелепами, трохи зачекати і повернутись до свого початкового вигляду.

```scratch

	коли одержую мене спіймали
	грати звук чавкання
	повторити 2
		змінити образ на закритий рот
		чекати 0.5 сек
		змінити образ на голодну рибу
	(кінець повторити)
```

__Тепер наша Голодна рибина готова їсти, тож наповнимо море здобиччю.  Натисніть правою кнопкою мишки на спрайт здобичі й продублюйте його кілька разів.__

###Протестуйте свій проект
Натисніть на зелени прапорець.  
Чи їсть Голодна рибина здобич? Чи їсть вона усіх спрайтів, що належать до здобичі?

Збережіть свій проект.

###Над цим варто подумати
WЧому нам потрібно додати блок "показати"  до скрипту спрайту здобичі?  Подумайте, що станеться, якщо здобич з'їдять.  Гра тоді припиниться до того, як здобич знову з'явиться.  Що тоді відбулося б, якби гру перезапустили?

__Молодець! Створення основної гри завершено, але є ще деякі речі, які можна зробити у грі.  Спробуй виконати ці бонусні завдання!__


##Бонусне завдання 1: Спрайти здобичі рухаються по-різному

Поки що уся здобич рухається однаково.  __Чи можна зробити так, щоб хтось із них рухався інакше?__
__Підказка:__ Не надто зациклюйтесь на цьому завданні, рухайтесь до інших.

__Оберіть один зі спрайтів здобичі для експерименту.__ Якщо у них однакові образи, змініть колір цього спрайту за допомогою блоку "встановити ефект колір".  Таким чином можна відрізнити цей спрайт серед інших.

Зробіть так, щоб цей спрайт рухався повільніше зі інші.  __Підказка:__ Зверніть увагу на блок "переміститись на (2) кроки".

Протестуйте свій проект.

Чи рухається здобич повільніше? Чи покращило це гру?  
Якщо зможете, зробіть так, щоб __одна рибина рухалась швидше за інших.__


Чи рухається здобич належним чином? Чи покращили зміни гру? 
__Підказка:__ Якщо здобич рухається колоподібно, перевірте значення змінних у блоці "вибрати випадкове від  до ".

Як щодо того, аби змінити рух кожного зі спрайтів здобичі, використовуючи різні комбінації змін?

Чи якісь з цих змін покращили гру? Чи стала гра цікавішою, веселішою, складнішою/простішою? Чи на краще це?

Збережіть свій проект.

##Бонусне завдання 2: Здобич вчиться втікати від  Голодної риби

Здобич у цій грі поводиться нерозумно.  Вона просто плаває, поки її не з'їдять.  У живій природі здобич втікає від хижака. Давайте зробимо так, щоб один із спрайтів здобичі втікав від Голодної риби.

У програмі Скретч немає блоків, які б інформували про напрям руху якогось із спрайтів.  Але можна зробити так, щоб один спрайт вказував на інший, а потім зробити так, щоб він повертав в інший бік.  Для цього знадобляться блоки вкладки Рух.

__Тож зробіть так, щоб один із спрайтів завжди вказував на Голодну рибу.__ Цей спрайт може погойдуватись, коли втікатиме.

###Протестуйте свій проект.
Чи стало тепер важче впіймати здобич? Чи стала гра кращою від цього?

Збережіть свій проект.

##Бонусне завдання 3: Додаємо в гру рахунок
Нецікаво просто поїдати здобич.  Як же дізнатись, хто з друзів краще грає у цю гру?  __Треба створити рахунок. Для цього додамо змінну рахунок через вкладку Величини.__ Прогляньте __скретч картку Рахунок__, щоб побачити, як це робиться. 

Де потрібно розмістити блок, який змінює кількість очок?

Переконайтесь, що рахунок повертається до 0 на початку гри.  Де слід розмістити відповідний блок?

###Протестуйте свій проект.
￼Чи повертається рахунок повертається до 0 на початку гри? Чи збільшується кількісь очок кожного разу, коли Голодна рибина з'їдає здобич?

Збережіть свій проект.

##Бонусне завадання 4: Додаємо в гру зворотній відлік.n

__Всановіть для себе часовий ліміт у грі.__ Скільки здобичі можна з'їси за 30 секунд? 

Прогляньте __скетч картку Таймер__, щоб дізнатись, як додати таймер у гру.  Почніть з гри, яка триває 30 секунд.

###Протестуйте свій проект.
Чи починається відлік на таймері з 30?

Чи з належною швидкістю йде зворотній відлік?

Чи можна ловити здобич, поки працює таймер?

Чи припиняється гра, коли на таймері 0?


Збережіть свій проект.

##Бонусне завдання 5: Додавання бонусних очок.e
Додайте великий бонус за поїдання трьох риб здобичі одночасно.  Як з'ясувати скільки риб одночасно з'їли?  __Підказка:__ можна створити  змінну  __"підрахувати"__, щоб порахувати скільки риб плаває у морі.

Збережіть свій проект.

##Бонусне завдання 6: Змінюємо гру: здобич повинна вижити! Іноді чудова нова ідея полягає в тому, щоб зробити щось цілком протилежне попередній.

__Змініть гру таким чином, щоб гравець замість того, щоб керувати Голодною рибиною і поїдати здобич, керував спрайтом здобичі, втікаючи від зграї Голодних риб.  Як довго вдається протриматись до того, як вас з'їли?__

Збережіть свій проект.

__Молодець! Проект завершено, тепер можна насолоджуватись грою!__
Не забудьте, що грою можна поділитись з друзями та рідними, натиснувши __"Поділитися цим проектом з іншими"__  у рядку меню. 
